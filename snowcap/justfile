set shell := ["bash", "-c"]

rootdir := justfile_directory()
xdg_data_dir := `echo "${XDG_DATA_HOME:-$HOME/.local/share}/pinnacle/snowcap"`

lua_version := "5.4"

list:
    @just --list --unsorted

install: install-protos gen-lua-pb-defs

install-protos:
    #!/usr/bin/env bash
    set -euxo pipefail
    proto_dir="{{xdg_data_dir}}/protobuf"
    rm -rf "${proto_dir}"
    mkdir -p "{{xdg_data_dir}}"
    cp -r "{{rootdir}}/api/protobuf" "${proto_dir}"

clean:
    rm -rf "{{xdg_data_dir}}"
    -luarocks remove --local snowcap-api

# Generate the protobuf definitions Lua file
gen-lua-pb-defs:
    #!/usr/bin/env bash
    set -euxo pipefail
    cargo build --package lua-build
    ../target/debug/lua-build ./api/protobuf > "./api/lua/snowcap/grpc/defs.lua"
